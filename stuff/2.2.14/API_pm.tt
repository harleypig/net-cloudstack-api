package Net::CloudStack::API;

# ABSTRACT: Basic request and response handling for calls to a CloudStack service.

use strict;
use warnings;
use namespace::autoclean;

use Carp;
use Net::CloudStack;
use Params::Validate ':all';
use Scalar::Util 'blessed';
use Sub::Exporter;

my @exports;

{ # Begin general hiding

##############################################################################
# Base structure

my $command = [% cmd_dump %];

##############################################################################
# Setup exports

my ( $exports, $groups, @all );

for my $cmd ( keys %$command ) {

  $exports->{ $cmd } = \&_generate_method;
  push @{ $groups->{ $command->{ $cmd }{ section } } }, $cmd;
  push @all, $cmd;

}

$groups->{ all } = \@all;

my $config = {

  exports => $exports,
  groups  => $groups,

};

Sub::Exporter::setup_exporter( $config );

##############################################################################
# Setup OO interface

# handle either a list of elements or a hashref

sub new { bless {}, ref $_[0] || $_[0] }

our $AUTOLOAD;

sub AUTOLOAD {

  my $self = $_[0];

  ( my $method = $AUTOLOAD ) =~ s/^.*:://;

  croak "unknown method $method"
    unless exists $command->{ $method };

  no strict 'refs';
  *$AUTOLOAD = $self->_generate_method( $method );

  goto &$AUTOLOAD;

}

sub DESTROY {}

##############################################################################
# Utility methods

sub _generate_method {

  my ( undef, $cmd ) = @_;

  croak "Unknown method: $cmd"
    unless exists $command->{ $cmd };

  # FIXME: allow caller to pass in their own validation structures.
  # FIXME: more robust handling of at least the obvious data types.
  my %validate;

  # Only build this part of the hash once ...
  $validate{ spec }{ $_ } = { type => SCALAR }
    for keys %{ $command->{ $cmd }{ request }{ required } };

  $validate{ spec }{ $_ } = { type => SCALAR, optional => 1 }
    for keys %{ $command->{ $cmd }{ request }{ optional } };

  return sub {

    my $self = '';
    $self = shift
      if blessed $_[0];

    $validate{ params } = \@_;

    my %arg;
    %arg = validate_with( %validate )
      if keys %{ $validate{ spec } };

    my @proc = $cmd;

    ( push @proc, join '&', map { "$_=$arg{$_}" } keys %arg )
      if keys %arg;

    my $api = blessed $self ? $self->api : api();

    $api->proc( @proc );

    return $api->send_request =~ /^yes$/i ? $api->response : $api->url;

  };
}

{ # Hide api stuff

  my $api;

  sub api {

    if ( ! @_ || ( @_ == 1 && blessed $_[0] eq __PACKAGE__ ) ) {

      croak "api not setup correctly"
        unless defined $api && blessed $api eq 'Net::CloudStack';

      return $api;

    }

    my $args = ( $_[0] eq __PACKAGE__ || blessed $_[0] eq __PACKAGE__ ) ? $_[1] : $_[0];

    croak 'args must be a hashref'
      if ref $args && ref $args ne 'HASH';

    $api = Net::CloudStack->new( $args )
      if ! defined $api || blessed $api ne 'Net::CloudStack';

    return $api
      unless ref $args;

    $api->$_( $args->{ $_ } )
      for keys %$args;

    return $api;

  }
} # End hiding api stuff

} # End general hiding

1;
